// Stolen from https://github.com/amber-lang/amber/blob/30ffefb823e94b903b82a0f01f14475ea4e297fe/setup/shared.ab

import { has_failed, env_const_get } from "std/env"
import { array_contains } from "std/array"

import { info, error } from "./utils.ab"
import { realpath } from "./path.ab"

pub fun get_os(): Text {
    // Determine OS type
    let os_type = $ uname -s $ failed {
        echo "Failed to determine OS type (using `uname` command)."
        echo "Please try again or make sure you have it installed."
        exit 1
    }
    if os_type == "Darwin":
        return "apple-darwin"
    
    if os_type != "Linux" {
        echo "Unsupported OS type: {os_type}"
        echo "Please try again or use another download method."
        exit 1
    }
    
    if not has_failed("ls -l /lib | grep libc.musl"):
        return "unknown-linux-musl"

    return "unknown-linux-gnu"
}

pub fun get_arch(): Text {
    // Determine architecture
    let arch_type = $ uname -m $ failed {
        echo "Failed to determine architecture."
        echo "Please try again or use another download method."
        exit 1
    }

    let arch = array_contains(["arm64", "aarch64"], arch_type)
        then "aarch64"
        else "x86_64"

    return arch
}

pub fun get_amethyst_data_dir(): Text {
    const home = trust env_const_get("HOME")
    const path = "{home}/.local/share/amethyst"

    $ mkdir -p "{path}" $ failed {
        error("Failed to create {path} directory.")
    }

    return realpath(path)
}
