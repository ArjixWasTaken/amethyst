import { array_shift, array_contains } from "std/array"
import { input_prompt } from "std/env"
import { dir_exists, file_exists, file_write, file_chmod } from "std/fs"
import { join } from "std/text"

import { info, fatal } from "../../../lib/utils.ab"
import { basename, realpath, is_dir_empty } from "../../../lib/path.ab"

import { get_amber_tags } from "../../../lib/github.ab"
import { get_local_versions, is_available_locally, download, delete_download, set_default } from "../../../lib/amber_manager.ab"

fun show_help_and_exit(code: Num = 0) {
    echo "Usage: amethyst amber use <version>"
    echo ""
    echo "Arguments:"
    echo "  <version>     The version of amber to use"
    echo ""
    echo "Options:"
    echo "  --help, -h    Show this help message and exit"
    exit(code)
}

pub fun cmd_amber_use(arguments: [Text]): Null {
    let version = ""

    loop {
        if len(arguments) == 0 {
            break
        }

        const arg = trust array_shift(arguments)
        if {
            arg == "--help" or arg == "-h": show_help_and_exit()
            else: version = arg
        }
    }

    if version == "" {
        show_help_and_exit(1)
    }

    const tags = get_local_versions()
    if not array_contains(tags, version) {
        info("Couldn't find version {version}, available versions are:")
        for tag in tags {
            echo "- {tag}"
        }

        exit(1)
    }

    echo "Using {version}"
    set_default(version)

    exit(0)
}
