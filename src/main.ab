import { array_shift } from "std/array"
import { file_exists, temp_dir_create } from "std/fs"
import { env_var_set, is_command, input_confirm } from "std/env"
import { join } from "std/text"

import { info, warn, fatal } from "../lib/utils.ab"
import { locate_project, open_ini, close_ini, get_project_property, get_project_amber, get_ini_value, set_ini_value } from "../lib/project.ab"
import { is_available_locally, is_available_remotely, download, execute_script } from "../lib/amber_manager.ab"

import { cmd_info } from "./commands/info.ab"
import { cmd_env } from "./commands/env.ab"
import { cmd_run } from "./commands/run.ab"
import { cmd_build } from "./commands/build.ab"
import { cmd_init } from "./commands/init.ab"
import { cmd_amber } from "./commands/amber/_mod.ab"
import { cmd_deps_add } from "./commands/deps/add.ab"
import { cmd_deps_remove } from "./commands/deps/remove.ab"
import { cmd_deps_install } from "./commands/deps/install.ab"

// TODO: Remove any unnecessary dependencies
const deps = ["uname", "curl", "jq", "python", "git"]
let missing = [Text]

for dep in deps {
    if not is_command(dep) {
        missing += [dep]
    }
}

if len(missing) > 0 {
    fatal("Missing dependencies: {join(missing, ",")}")
}

fun show_help_and_exit(code: Num = 0) {
    echo "Usage: amethyst <command> [options]"
    echo ""
    echo "Commands:"
    echo "  init       Bootstrap a new amber project"
    echo "  info       Display information about the current amber project"
    echo "  add        Add a dependency to amethyst.ini"
    echo "  remove     Remove a dependency from amethyst.ini"
    echo "  install    Install the dependencies defined in amethyst.ini"
    echo "  run        Run current project"
    echo "  build      Build current project"
    echo "  env        Generate bash env"
    echo "  amber      Manage amber versions"
    echo "  deps       Manage dependencies"
    echo ""
    echo "Options:"
    echo "  --help, -h    Show this help message and exit"
    exit(code)
}

main(args) {
    let arguments = args
    trust array_shift(arguments)

    const arg = trust array_shift(arguments)

    const project = locate_project()

    if project[0] == "yes" {
        const tmp_dir = temp_dir_create("amethyst.XXXXXXXXXX", true, true)?
        env_var_set("AM_INI_DIR", tmp_dir)?
        open_ini("{project[1]}/amethyst.ini")
    }

    if file_exists(arg) {
        if project[0] != "yes" {
            fatal("Cannot run scripts from outside of amethyst project")
        }

        const version = get_project_amber()
        if is_available_locally(version) {
            execute_script(version, arg, arguments)
            // execute_script uses exec, so really, this *is* unreachable
            // but just in case
            exit(0)
        }
    }

    if {
        arg == "--help":  show_help_and_exit()
        arg == "amber":   cmd_amber(arguments)
        arg == "add":     cmd_deps_add(arguments)
        arg == "remove":  cmd_deps_remove(arguments)
        arg == "install": cmd_deps_install(arguments)
        
        arg == "init":    cmd_init(arguments)
        arg == "info":    cmd_info(arguments)
        arg == "env":     cmd_env(arguments)
        arg == "build":   cmd_build(arguments)
        arg == "run":     cmd_run(arguments)
        else: show_help_and_exit(1)
    }
}
